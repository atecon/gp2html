set verbose off
include gp2html.gfn

# Set your own workdir for storing the generated files
set workdir "/home/artur/git/gp2html"

open AWM18.gdt --quiet

strings Filenames = empty

# Generate the SVG plots
list L = YER URX

## non-interactive plot
string filename = sprintf("plot1.svg")
Filenames += filename
gnuplot L --with-lines --time-series --output="@filename"

## interactive plot
string filename = sprintf("plot2.svg")
Filenames += filename
gnuplot L --fit=linear --output="@filename" { set title "Linear Fit"; set key bottom right;\
    set terminal svg standalone mouse; set grid;}


## also interactive plot but with jsdir being explicitly specified

OPTION = 1  # select an option: 1 or 2

if OPTION == 1  # use local jsdir
  string JSDIR = "/usr/local/share/gnuplot/5.4/js/"  # ! Specify your local path !
  string standalone_opt = "standalone"  # keyword needed

elif OPTION == 2  # use online jsdir
  string JSDIR = "https://gnuplot.sourceforge.net/demo_svg_5.4"  # Specify the online path
  string standalone_opt = ""      # the keyword is not needed
endif

string filename = sprintf("plot3.svg")
Filenames += filename

gnuplot L --with-lines --time-series --output="@filename" \
  { set terminal svg mouse @standalone_opt jsdir "@JSDIR";\
    set grid; set key bottom right;}


# Generate the HTML file
string filename = "index_default.html"
gp2html(Filenames, filename)


# Tweak some parameters and generate another HTML site
bundle Params = _(global_title = "Overview of plots",
                  single_column = FALSE,
                  titles = defarray("Not interactive", "Interactive", "Also interactive"),
                  note = "Add some note below the top title",
                  show_howto = FALSE)

string filename = "index_tweaked.html"
gp2html(Filenames, filename, Params)

